varnishtest "Verify simple requests."

server s1 {
    rxreq
    expect req.url == "/"
    txresp

} -start

server s2 {
    rxreq
    expect req.url == "/"
    txresp

} -start

server s3 {
    rxreq
    expect req.url == "/"
    txresp

    rxreq
    expect req.url == "/"
    txresp

} -start

varnish v1 -vcl {

    backend b1 {
               .host = "${s1_addr}";
               .port = "${s1_port}";
    }

    sub vcl_recv {
        if (req.method == "PURGE") {
            return(purge);
        }
    }
} -start

varnish v2 -vcl {

    backend b1 {
               .host = "${s2_addr}";
               .port = "${s2_port}";
    }

    sub vcl_recv {
        if (req.method == "PURGE") {
            return(purge);
        }
    }
} -start

varnish v3 -vcl {

    backend b1 {
               .host = "${s3_addr}";
               .port = "${s3_port}";
    }

    sub vcl_recv {
        if (req.method == "PURGE") {
            return(purge);
        }
    }
} -start

shell {
    echo "[test]"			> nodes.conf
    echo "Cache1 = http://${v1_addr}:${v1_port}"	>> nodes.conf
    echo "Cache2 = http://${v2_addr}:${v2_port}"	>> nodes.conf
    echo "Cache3 = http://${v3_addr}:${v3_port}"	>> nodes.conf
}

process p0 {
    ${broadcaster} -cfg nodes.conf
} -start

delay 0.2

# Broadcast a GET request, warm up the caches.
client c1 -connect 127.0.0.1:8088 {
    txreq -url "/" -hdr "x-group: test" -hdr "Host: localhost"
    rxresp

    expect resp.status == 200
    expect resp.bodylen == "53"
} -run

# Broadcast againt a non-existing group.
client c2 -connect 127.0.0.1:8088 {
    txreq -url "/" -hdr "x-group: bogus" -hdr "Host: localhost"
    rxresp

    expect resp.status == 404
} -run

# Check that there is some cached content.
# A value different from 0 for the age header
# should be a good enough indicator.
client c3 -connect ${v3_sock} {
    txreq -url "/"  -hdr "Host: ${v3_addr}:${v3_port}"
    rxresp

    delay 1

    txreq -url "/"  -hdr "Host: ${v3_addr}:${v3_port}"
    rxresp

    expect resp.status == 200
    expect resp.http.age == 1
} -run

# Clear the caches.
client c4 -connect 127.0.0.1:8088 {
    txreq -req PURGE -url "/" -hdr "x-group: test" -hdr "Host: localhost"
    rxresp

    expect resp.status == 200
    expect resp.bodylen == "53"
} -run

# Check that after the purge the Age header
# has been reset to 0.
client c5 -connect ${v3_sock} {
    txreq -url "/"  -hdr "Host: ${v3_addr}:${v3_port}"
    rxresp

    expect resp.status == 200
    expect resp.http.age == 0
} -run
