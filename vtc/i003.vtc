# The configuration reload is being triggered
# when the broadcaster process receives
# a SIGHUP notification.

varnishtest "Verify configuration reload."

# Prepare some configuration.
# We start with a faulty configuration
shell {
    echo "[test]"				> nodes.conf
    echo "Cache1 = http://${bad_ip}:80"		>> nodes.conf
}

process p0 {
    ${broadcaster} -cfg nodes.conf --enforce
} -start

server s1 {
    rxreq
    expect req.url == "/"
    txresp

} -start

varnish v1 -vcl {

    backend b1 {
               .host = "${s1_addr}";
               .port = "${s1_port}";
    }

    sub vcl_recv {
        if (req.method == "PURGE") {
            return(purge);
        }
    }
} -start

client c1 -connect 127.0.0.1:8088 {
    txreq -req PURGE -url "/" -hdr "Host: localhost" -hdr "x-group: test"
    rxresp

    expect resp.status == 500
    expect resp.bodylen == "19"
} -run


# Rewrite the config file with a valid cache and signal
# the broadcaster to reload.
shell {
    rm -f nodes.conf
    echo "[test]"					> nodes.conf
    echo "Cache1 = http://${v1_addr}:${v1_port}"	>> nodes.conf
    kill -1 ${p0_pid}
}

# Check that the configuration has been reloaded and
# that this time the request returns a 200.
client c1 -connect 127.0.0.1:8088 {
    txreq -req PURGE -url "/" -hdr "Host: localhost" -hdr "x-group: test"
    rxresp

    expect resp.status == 200
    expect resp.bodylen == "19"
} -run
